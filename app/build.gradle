plugins {
    id 'java'
    id 'jacoco'
    id 'application'
    id 'io.ebean' version '12.16.1'
}

group 'hexlet.code'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

compileJava {
    options.release = 17
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'hexlet.code.App'
}

dependencies {

    // Библиотеки javalin (только версия 4.х.х)
    implementation 'io.javalin:javalin:4.6.7'
    implementation 'org.slf4j:slf4j-simple:2.0.5'

    // Библиотеки thymeleaf (только версия 3.0.х)
    implementation 'org.thymeleaf:thymeleaf:3.0.15.RELEASE'
    // позволяет создавать общие макеты и переиспользовать их
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.1.0'
    // добавляет в шаблоны объект temporals со множеством полезных методов по форматированию
    implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.4.RELEASE'


    // ORM ebean
    implementation 'io.ebean:ebean:12.16.1'
    implementation 'io.ebean:ebean-querybean:12.16.1'
    implementation 'io.ebean:ebean-migration:12.16.1'
    implementation 'io.ebean:ebean-annotation:7.8'
    implementation 'io.ebean:ebean-ddl-generator:12.16.1'
    annotationProcessor 'io.ebean:querybean-generator:12.16.1'
    // NOTE: ebean-ddl-generator требует старые зависимости, которых нет в java 9 и выше
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.5'
    implementation 'javax.activation:activation:1.1.1'

    // БД типа postgresql
    implementation 'org.postgresql:postgresql:42.5.1'
    // БД типа H2
    implementation 'com.h2database:h2:2.1.214'

    // оформления страниц веб-приложения
    implementation 'org.webjars:bootstrap:5.1.1'

    // библиотеки для тестирования веб
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'io.ebean:ebean-test:12.11.5'
    testImplementation 'com.konghq:unirest-java:3.14.1'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Main-Class': 'hexlet.code.App'
        )
    }
    from { (configurations.runtimeClasspath).collect { it.isDirectory() ? it : zipTree(it) } }
}

task generateMigrations(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'hexlet.code.MigrationGenerator'
}